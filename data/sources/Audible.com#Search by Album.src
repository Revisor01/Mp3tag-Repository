# ###################################################################
#
# Mp3tag parsing for Audible.com, created by dano on 2013-03-11
#
# 2013-09-20: Updated (dano)
# 2020-03-24: Updated (dano)
# 2020-04-06: Updated to Audible.com (seanap)
# 2020-04-11: Several changes, see the commits (romano)
# 2020-04-14: Several changes, see the commits (romano)
#
# This file should be in your sources dir. 
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#
#


[Name]=Audible.com
[BasedOn]=www.audible.com
[IndexUrl]=https://www.audible.com/search?keywords=
[AlbumUrl]=https://www.audible.com
[WordSeperator]=+
[IndexFormat]=%_url%|%Album%|%Author%|%Duration%|%Year%|%Language%
[SearchBy]=%artist% $regexp(%album%,'[- ]+cd ?\d+$',,1)
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\xxxxx\Desktop\mp3tagdebug.txt"


#join everything until end 
joinuntil "</html>"

#remove spaces after lines
regexpreplace "[\r\n]+" ""

#.+ Matches any character except linebreaks and replace them whit "results"
regexpreplace ".+results" "results"

regexpreplace "<script asyn.+" ""

#Matches any whitespace character (spaces, tabs, line breaks).
regexpreplace "\s\s+" " "

#Matches a TAB character
regexpreplace "\t+" " "
replace "\" >" "\">"
replace "> <" "><"
replace "Running Time:" ""


findinline "results"
findinline "<h3 class=\"bc-heading"


#this \" is iqual to "
do	
	#  Url		
	findinline "href=\""
	sayuntil "\""
	say "|"
	
	# Album
	findinline ">"
	sayuntil "<"
	
	findinline "</li>"
	if "<li class=\"bc-list-item subtitle"
		say " - "
		findinline "bc-color-secondary\">"
		sayuntil "<"
		
	else
		if  "<li class=\"bc-list-item bc"
			say " - "
			findinline ">"
			sayuntil "<"
		endif
	
	endif
	say "|" 
	
	#Author
	findinline "authorLabel\">"
	findinline "href="
	findinline ">"
	sayuntil "<"
	say "|"

	
    # Duration
    findinline "runtimeLabel\">"
    findinline "bc-color-secondary\">"
    sayuntil "<"
    say "|"
	
    # Year
    findinline "Release date:"
	sayuntil "<"
	say "|"
   
    # Language
	findinline "languageLabel\">"
	findinline "Language:"
	sayuntil "<"

    
	saynewline
	 
   	findinline "<h3 class=\"bc-heading " 1 1
    
while "bc-color-link" 99


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
#DebugWriteInput "C:\Users\xxxxx\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\xxxxx\Desktop\mp3tagdebug.txt"


# Asin
findline "\"productAsin\" value=\""
findinline "\"productAsin\" value=\""
outputto "Asin"
sayuntil "\""
gotoline 1

# Album
findline "<h1  class=\"bc-heading"
joinuntil "authorLabel"
findinline ">"
findinline ":" 1 1
movechar -1
if ":"
	findline "<h1  class=\"bc-heading"
	joinuntil "authorLabel"
	findinline ">"
	outputto "Album"
	sayuntil ":"
	outputto "subtitle"
	movechar 2
	sayuntil "<"
else
	findline "<h1  class=\"bc-heading"
	joinuntil "authorLabel"
	findinline ">"
	outputto "Album"
	sayuntil "<"
endif




# Subtitle of Album
regexpreplace "  +" " "
replace "<span class=\"bc-text bc-size-medium\" ></span>" ""
findinline "bc-text bc-size-medium\" " 1 1
if ">"
	movechar 1
	outputto "Subtitle"
	sayuntil "</"
else
	gotoline 1
endif

# Author
outputto "Artist"
findline "authorLabel"
moveline 1 1
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
regexpreplace ".+By:" ""
sayrest

# narratorLabel
outputto "Composer"
findline "narratorLabel"
moveline 1 1
joinuntil "</span>"
#remove the </b> and all variations
regexpreplace "</?[^><]+>" ""
unspace
#join more than one space
regexpreplace "  +" " "
regexpreplace ".+Narrated by:" ""
sayrest

# Grouping / Series
findline "seriesLabel" 1 1
unspace
if "seriesLabel"
	outputto "SERIES"
	findline "href="
	findinline ">"
	sayuntil "<"
	outputto "series-part"
	findinline "a>, Book " 1 1
	SayRegexp "^\d+"
else
	gotoline 1
endif

# 1st Genre
outputto "tmp_Genre1"
findline "categoriesLabel"
joinuntil "</span>"
findinline "href="
findinline ">"
sayuntil "<"

# 2nd Genre
findinline "href=" 1 1
movechar -1
if "="
	findinline ">"
	outputto "tmp_Genre2"
	sayuntil "<"
	outputto "genre"
	sayoutput "tmp_Genre1"
	say "/"
	sayoutput "tmp_Genre2"
else
	outputto "genre"
	sayoutput "tmp_Genre1"
endif



# Rating
findline "ratingsLabel"
joinuntil "adbl-buy-box-area"
findinline "<span class=\"bc-text\" " 1 1
movechar -22
if "<span class=\"bc-text\" "
	findinline ">"
	outputto "RATING WMP"
	sayuntil "<"
	findinline ">"
	sayuntil "<"
else
endif
outputto "RATING WINAMP"
sayoutput "RATING WMP"

# Description
findline "Publisher's Summary</h2>"
outputto "Comment"
findline "<span class"
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
replace "bc-color-secondary\" >" ""
sayrest

# Year
findline "©"
findinline "©"
outputto "Year"
SayNChars 4
gotoline 1

# ©, Release Year, Publisher
findline "©"
replace "(P)" "; "
findinline "©"
outputto "Copyright"
sayuntil ";"
movechar 2
outputto "RELEASETIME"
SayNChars 4
outputto "Publisher"
movechar 1
sayuntil "<"

# Cover
outputto "Coverurl"
findline "\"image\": \""
replace "_SL175_" "_SS500_"
replace "_SL300_" "_SS500_"
findinline "\"image\": \""
sayuntil "\""

# Set "Albumsort"
IfOutput "series"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "series-part"
	say " "
	say "- "
	sayoutput "Year"
	say ". "
	sayoutput "album"
else
	outputto "Albumsort"
	sayoutput "Year"
	say ". "
	sayoutput "album"
endif


# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# Set Disknumber
outputto "DISCNUMBER"
sayoutput "series-part"

# Set Audible Album URL
outputto "WWWAUDIOFILE"
sayoutput "CurrentUrl"

# Set Comment to DESCRIPTION for better MP4 support
outputto "DESCRIPTION"
sayoutput "Comment"