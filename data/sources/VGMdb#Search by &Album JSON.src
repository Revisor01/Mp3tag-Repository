# ######################################################################
# Mp3tag parsing for VGMdb.net, created by romano on 2010-08-13
#
#
# Copy this file to your sources directory at
# %APPDATA%\Mp3tag\data\sources
#
# Changes in the commits: https://github.com/romanoh/Mp3tag-Repository
# Last update 04.12.2020
#
# ######################################################################

[Name]=VGMdb [v.04.12.2020]
[BasedOn]=vgmdb.info
[IndexUrl]=http://vgmdb.info/search?q=
[AlbumUrl]=http://vgmdb.info/
[WordSeparator]=+
[IndexFormat]=% ↓ Catalog%|%_url%|% ↓ Year%|% ↓ Album%
#remove everything inside ()
[SearchBy]=Enter Album Name||$regexp(%album%,'\([^)]*\)',,1)||%s
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#Debug "ON" "C:\Users\xxx\Desktop\mp3tag-json-index-debug.txt"

json "ON" 

json_select "results"
	json_foreach "albums"

		# Catalog
		json_select "catalog"
		SayRest
		Say "|"

		# Link
		json_select "link"
		SayRest
		Say "|"
		
		# Year
		json_select "release_date"
		SayRest
		Say "|"
		
		# Album
		json_select "titles"
		json_select "en"
		SayRest
		
		SayNewline

	json_foreach_end
	

################################################################################
#                        ADJUST TAG INFORMATION DIALOG                         #
################################################################################
[ParserScriptAlbum]=...

## Comment/uncomment for debugging purposes:
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag-deezer-ws-album-debug.out.html"
#Debug "ON" "C:\Users\xxxx\Downloads\vocadb.json"

json "ON"

# Arrangers
outputto "CONDUCTOR"
json_foreach "arrangers"
	# ifnot "0" is for when there is no data or lenght of array, The index starts at zero and not one. Few languages use 1 as the first index.
	ifnot "0"
		json_select "names"
		json_select "en"
		sayrest
		say ", "
	else
		say ""
	endif
json_foreach_end

# Catalog
OutputTo "Catalog"
json_select "catalog"
SayRest

# notes
OutputTo "notes"
json_select "category"
SayRest

# Involved People
json_foreach "organizations"
ifnot "0"
	OutputTo "INVOLVEDPEOPLE"
	json_select "role"
	ifnot ""
		sayrest
		say ":"
		json_select "names"
		json_select "en"
		sayrest
		say " "
	else
		say ""
	endif
		
else
		say ""
endif
json_foreach_end

# Genre
OutputTo "genre"
json_select "classification"
ifnot "0"
	SayRest
else
	say ""
endif

# Album
OutputTo "ALBUM"
json_select "name"
SayRest
# Artist 
outputto "artist"
json_foreach "performers"
ifnot "0"
		json_select "names"
		json_select "en"
		sayrest
		say ", "
else
		json_foreach "composers"
			ifnot "0"
			
				json_select "names"
				json_select "en"
				sayrest
				# Adding this below will output all the artists(composers) but if omitted it will only get the first array.
				say ", "
			else
				say ""
			endif
		json_foreach_end
endif
json_foreach_end		
		
# Composers
outputto "composer"
json_foreach "composers"
			ifnot "0"
			
				json_select "names"
				json_select "en"
				sayrest
				say ", "
			else
				say ""
			endif
json_foreach_end

# Tracks English to Japanese to Romaji
json_foreach "discs"
		# Title
		OutputTo "Title"
		json_foreach "tracks"
			json_select "names"
			json_select "English"
			ifnot ""
				SayRest
				say "|"
			else
				json_select "Japanese"
					ifnot ""
						SayRest
						say "|"
					else
						json_select "Romaji"
							ifnot ""
								SayRest
								say "|"
							else
							say "no songs names"
							endif
					endif
			endif
			
		json_foreach_end	
json_foreach_end		
		
# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# LYRICIST
OutputTo "LYRICIST"
json_foreach "lyricists"
	ifnot "0"
		json_select "names"
		json_select "en"
		
		sayrest
		
		say " "
	else
		say ""
	endif
json_foreach_end

# Media type
OutputTo "MEDIATYPE"
json_select "media_format"
SayRest
	
# Comments
OutputTo "comment"
json_select "notes"
SayRest	
	
# Cover
OutputTo "COVERURL"
json_select "picture_full"
SayRest
## You can choose the cover resolution here
# json_select "picture_small"
# json_select "picture_thumb"

# Publish_format
OutputTo "format"
json_select "publish_format"
SayRest
	
# Rating
OutputTo "rating"
json_select "rating"
ifnot "0"
	SayRest	
else
	say ""
endif

# Release_date
OutputTo "YEAR"
json_select "release_date"
SayRest	
## B) Year only, e.g. 2018
#SayUntil "-"

# vgmdb_link
OutputTo "www"
json_select "vgmdb_link"
SayRest

# Event
outputto "Event"
json_foreach "release_events"
	ifnot "0"
		json_select "name"
		sayrest
	else
		say ""
	endif
json_foreach_end