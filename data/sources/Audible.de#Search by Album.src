# ###################################################################
#
# Mp3tag parsing for Audible.de, created by dano on 2013-03-11
#
# 2013-09-20: Updated
# 2020-03-24: Updated
# 2020-04-04: Add Year
# 2020-04-05: Change year to Book release year, Add audiobook Release year(RELEASETIME), ALBUMARTIST is now the same of ARTIST. Add Lenght. Remove the "English - " in genre.
#
# This file should be in your sources dir. 
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#
#


[Name]=Audible.de
[BasedOn]=www.audible.de
[IndexUrl]=https://www.audible.de/search?keywords=
[AlbumUrl]=https://www.audible.de
[WordSeperator]=+
[IndexFormat]=%_url%|%Album%|%Author%|%Duration%|%Release year%
[SearchBy]=$regexp(%album%,'[- ]+cd ?\d+$',,1)
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\YOURNAME\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\YOURNAME\Desktop\mp3tagdebug.txt"


#join everything until end 
joinuntil "</html>"

#remove spaces after lines
regexpreplace "[\r\n]+" ""

#.+ Matches any character except linebreaks and replace them whith "results"
regexpreplace ".+Ergebnissen" "Ergebnissen"
regexpreplace "<script asyn.+" ""

#Matches any whitespace character (spaces, tabs, line breaks).
regexpreplace "\s\s+" " "

#Matches a TAB character
regexpreplace "\t+" " "
replace "\" >" "\">"
replace "> <" "><"
replace "Spieldauer:" ""


findinline "Ergebnissen"
findinline "<h3 class=\"bc-heading"

#this \" is iqual to "

do	
	#  Url		
	findinline "href=\""
	sayuntil "\""
	say "|"
	
	# Album
	findinline ">"
	sayuntil "<"
	
	findinline "</li>"
	if "<li class=\"bc-list-item subtitle"
		say " - "
		findinline "bc-color-secondary\">"
		sayuntil "<"
		
	else
		if  "<li class=\"bc-list-item bc"
			say " - "
			findinline ">"
			sayuntil "<"
		endif
	
	endif
	say "|" 
	
	#Author
	findinline "authorLabel\">"
	findinline "href="
	findinline ">"
	sayuntil "<"
	say "|"

	
    # Duration
    findinline "runtimeLabel\">"
    findinline "bc-color-secondary\">"
    sayuntil "<"
    say "|"
	
	#this is release year, the release book year will go to YEAR tag.
    # Release Year
    findinline "Erscheinungsdatum:"
	sayuntil "<"
   
    

    
	saynewline
	 
   	findinline "<h3 class=\"bc-heading " 1 1
    
while "bc-color-link" 99


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################



# Asin
findline "\"productAsin\" value=\""
findinline "\"productAsin\" value=\""
outputto "Asin"
sayuntil "\""
gotoline 1

# Album
outputto "Album"
findline "<h1  class=\"bc-heading"
joinuntil "authorLabel"
findinline ">"
sayuntil "<"

# Subtitle of Album
regexpreplace "  +" " "
replace "<span class=\"bc-text bc-size-medium\" ></span>" ""
findinline "bc-text bc-size-medium\" " 1 1
if ">"
	movechar 1
	outputto "Album"
	say " - "
	sayuntil "</"
else
    gotoline 1
endif



# Author
outputto "Artist"
findline "authorLabel"
moveline 1 1
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
regexpreplace ".+Autor:" ""
sayrest


# Grouping / Serie
findline "seriesLabel" 1 1
unspace
if "seriesLabel"
	outputto "CONTENTGROUP"
	findline "href="
	findline ">"
	findinline ">"
	sayuntil "<"
else
	gotoline 1
endif

# Lenght
outputto "LENGTH"
findline "Spieldauer:" 1 1
findinline "Spieldauer:"
moveline 1
regexpreplace " Std. und " ":"
sayuntil "</span>"

# Genres
outputto "Genre"
findline "categoriesLabel"
joinuntil "</span>"
replace "English - " ""
findinline "href="
findinline ">"
sayuntil "<"

# 2nd Genre
outputto "Category"
findinline "href="
findinline ">"
sayuntil "<"

# Description
findline "Inhaltsangabe</h2>"
outputto "SUBTITLE"
findline "<span class"
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
replace "bc-color-secondary\" >" ""
sayrest

# RELEASETIME
findline "©"
outputto "year"
findinline "©"
SayNChars 4

# Year
findline "(P)"
outputto "RELEASETIME"
findinline "(P)"
SayNChars 4

# Cover
outputto "Coverurl"
findline "\"image\": \""
replace "_SL175_" "_SS500_"
replace "_SL300_" "_SS500_"
findinline "\"image\": \""
sayuntil "\""

#set "Category"

#Send the content of output S to the current output.
#set "Albumsort"
outputto "Albumsort"
Sayoutput "Year"
sayoutput "series"
say " "
sayoutput "series-part"
say " - "
sayoutput "album"

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"
