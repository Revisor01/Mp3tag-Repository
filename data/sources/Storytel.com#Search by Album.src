# ###################################################################
#
# Mp3tag parsing for storytel.com, created by romano on 2020-11-28
#
# Changes in the commits: https://github.com/romanoh/Mp3tag-Repository
# Last update: 27.12.2020 v.0.8
# 04.12.2020 - Fix sort album tag if the album is not from a series
# 27.12.2020 - Improve Lenght display
#
# ####################################################################

[Name]=Storytel [v.0.8-27.12.2020]
[BasedOn]=www.storytel.com
[IndexUrl]=https://www.storytel.com/de/de/suche-
[AlbumUrl]=https://www.storytel.com
[WordSeperator]=+
[IndexFormat]=% ↓ Sprache%|% ↓ Album%|% ↓ Verfasser:in%|% ↓ Art%|%_url%
[SearchBy]=Enter Album||$regexp(%album%,'[- ]+cd ?\d+$',,1)||%s
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\xxxx\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\xxxx\Desktop\mp3tagdebug.txt"

#Only select the area we need instead of everyting.
findline "gridItem"
joinuntil "hiddenSearchPhrase"

replace "asAudio" "Hörbuch"
replace "asEbook" "Ebook"

do
	# Language
	findinline "language=\""
	sayuntil "\""
	say "|"

	# Album
	findinline "name=\""
	sayuntil "\""
	say "|"

	# Author
	findinline "authors=\""
	sayuntil "\""
	say "|"

	# Type
	findinline "<span class=\""
	sayuntil "\""
	say "|"

	#  Url
	findinline "gridBookTitle"
	findinline "href=\""
	sayuntil "\""
	#the last on remove the #
	#say "|"

	saynewline

   findinline "gridItem" 1 1

while "\"" 25

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
DebugWriteInput "C:\Users\simonluthe\Desktop\mp3tag.html"
Debug "ON" "C:\Users\simonluthe\Desktop\mp3tagdebug.txt"



# Cover
outputto "Coverurl"
findline "book-cover"
findline "src=\""
replace "/images/200x200/" "/images/200x200/"
findinline "src=\""
sayuntil "\""

# Album
outputto "Album"
findline "heading--title-large"
findinline ">"
findinline ":"
sayuntil "</h1>"

# Title
outputto "Title"
findline "heading--title-large"
findinline ">"
findinline ":"
sayuntil "</h1>"

# Author/Artist
outputto "Artist"
findline "href=\"/de/de/authors/"
findinline "<a"
findinline "\">"
sayuntil "</a>"

# Narrator

outputto "Composer"
findline "href=\"/de/de/narrators/"
joinuntil "</dd>"
regexpreplace "<[\w\d]+ [^>]*?>" ""
regexpreplace "<\/[\w\- _]*?>" ""
sayuntil "</dd>"


# Set Artist = Albumartist
outputto "Albumartist"
sayoutput "Artist"

#%artist%=regexp(%artist%,(\b\w+\b)(?=[\s\S]*\b\1\b).+, )

# Description
outputto "Comment"
findline "book-description"
replace "<p>" ""
replace "</p>" ""
findinline "<article"
findinline "\">"
sayuntil "</article>"


outputto "DESCRIPTION"
sayoutput "Comment"

# Genre
outputto "Genre"
findline "Kategorie:"
findline "href=\"/de/de/categories/"
findinline "<a"
findinline "\">"
sayuntil "</a>"

outputto "SERIES"
findline "Serien:" 1 1
if "<dd"
findline "href=\"/de/de/series/"
joinuntil "</a>"
findinline "href=\"/de/de/series/"
findinline "\">"
sayuntil  ":"

outputto "TRACK"
findinline ":"
SayNextNumber
sayuntil  "</a>"
else
	say ""
	gotoline 1
endif

#Publisher
outputto "Publisher"
findline "Verlag:"
findline "href=\"/de/de/publishers/"
findinline "<a"
findinline "\">"
sayuntil "</a>"

outputto "Copyright"
sayoutput "Publisher"

outputto "Label"
sayoutput "Publisher"

# Year
outputto "year"
findline "Veröffentlicht:"
moveline 1
findinline "\">"
SayUntil "-"

outputto "Year"
sayoutput "RELEASETIME"

# Lenght
outputto "lenght"
findline "Länge:"
moveline 1
replace "Std. " ":"
replace "Min" ""
findinline "\">"
SayUntil "</dd>"

#ISBN
outputto "ISBN"
findline "ISBN:"
findinline "ISBN:"
moveline 1
findinline "\">"
SayUntil "</dd>"


# Set "Albumsort"
IfOutput "series"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "track"
	say " "
	say "- "
	sayoutput "title"
else
	outputto "Albumsort"
	sayoutput "Year"
	say ". "
	sayoutput "album"
endif

# Set Disknumber

outputto "series-part"
sayoutput "TRACK"
outputto "DISCNUMBER"
sayoutput "series-part"

# Set Audible Album URL
outputto "WWWAUDIOFILE"
sayoutput "CurrentUrl"

# Set Subtitle
outputto "Subtitle"
sayoutput "series"
say " "
sayoutput "track"

# Set iTunes Media Type to Audiobook
outputto "ITUNESMEDIATYPE"
say "Audiobook"

# Set iTunes Gapless Playback
outputto "ITUNESGAPLESS"
say "1"
