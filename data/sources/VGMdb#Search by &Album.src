# ###################################################################
# Mp3tag (v2.39+) parsing for VGMdb.net, created by dano on 2010-01-20
#
# v5 from 2019-05-28
#
# Copy this file to your sources directory at
# %APPDATA%\Mp3tag\data\sources
#
# Changelog:
# [2020-04-27] change somethings (by romano)
# [2019-03-16] FIX: Replaced HTTP by HTTPS (by axxeldna)
# [2015-09-01] FIX: Coverurl fixed (by PBX_g33k)
# 
# ###################################################################

[Name]=VGMdb - Search by Album
[BasedOn]=vgmdb.net
[IndexUrl]=https://vgmdb.net/search?q=
[AlbumUrl]=https://vgmdb.net/album/
[WordSeperator]=+
[IndexFormat]=%Catalog%|%_url%|%Album%|%Year%
#remove everything inside ()
[SearchBy]=$regexp(%album%,'\([^)]*\)',,1)
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################


findline "<tr rel='rel_" 1 1

ifnot "<tr rel"
	say "|"
	sayoutput "CurrentURL"
	say "|"
	say "|"
	exit
endif

do        
    replace "|" "/"
    findinline "span class="
    findinline ">"
    sayuntil "</span"
    say "|"
    
	# URL
    findinline "https://vgmdb.net/album/"
    sayuntil "\""
    say "|"
    
    # Album
    findinline "class=\"albumtitle\""
    findinline "\">"
    sayuntil "</span>"
    say "|"
    
    # Year
    findinline "</tr>"
    movechar -1
    findinline "\"" -1
    findinline ">"
    sayuntil "</"
    saynewline
    
    moveline 1 1
    unspace
while "<tr rel='rel_" 99


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
Debug "ON" "C:\Users\egual\Desktop\mp3tagdebug2.txt"
# Album
outputto "Album"
findline "class=\"albumtitle\""
replace "|" "$verticalBar()"
findinline "class=\"albumtitle\""
findinline ">"
sayuntil "</span"

# Coverurl
outputto "coverurl"
findline "id=\"coverart\""
findinline "style=\"background-image: url("
#get full image
#replace "medium-" ""
#findinline "style=\"background-image: url("
if "'"
	movechar 1
	sayuntil "'"
	
else
	findinline "../"
	sayuntil "\""
endif


# Catalog
outputto "Catalog"
findline "<b>Catalog Number</b>"
joinuntil "</tr>"
findinline "<td width=\"100%\">"
ifnot "N/A"
	if "<span id="
		findinline "#\">"
		sayuntil "</a>"
	else
		regexpreplace "<a[^>]+>" ""
		replace "</a>" ""
		findinline "<td width=\"100%\">"
		sayuntil "</td>"
	endif
endif

# Release Date
outputto "Year"
findline "<b>Release Date</b>"
moveline 1
sayregexp "(?<=<td>)\d{4} *(?=</td>)" "" 

if "<td><a "
	findinline "title=\"View albums released on"
	sayregexp "\d{4}(?=\" href)" "" "</a>"
endif


# Publish Format
outputto "Format"
findline "<b>Publish Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Media Format
outputto "Mediatype"
findline "<b>Media Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Classification (Genre)
outputto "Genre"
findline "<b>Classification</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"


# Publisher
findline "id=\"publisher_less\""	
outputto "Publisher"
findinline "display:inline\">"
sayuntil "</span>"

# Composer
findline "<b>Composed by</b>"
outputto "Composer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Arranged
findline "<b>Arranged by</b>"
outputto "Writer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Performed
findline "<b>Performed by</b>" 1 1
outputto "Artist"
moveline 1
findline "\">"
findinline "\">"
sayuntil "</a>"



# Tracks
# findline "<b>Disc 2</b></span>"
findline "class=\"smallfont\"><span class=\"label\">"
do
	outputto "Tracks"
	moveline 1
	findinline "width=\"100%\">"
	sayuntil "</td>"
	say "|"
	
	outputto "_Length"
	moveline 1
	sayregexp "(?<=<span class=\"time\">)[\d:]+(?=</span>)" ""
	say "|"

	findline "class=\"smallfont\"><span class=\"label\">" 1 1	
while "<td "

# Notes
gotoline 1
outputto "Notes"
findline "<h3>Notes</h3>"
findline "id=\"notes\">"
regexpreplace "<br />" " "
findinline "id=\"notes\">"
sayuntil "</div>"


# VGMdb Album URL
outputto "VGMdb Album Url"
sayoutput "CurrentUrl"

# this site dont have artist weird
# Set artistname
#outputto "artist"
#sayoutput "composer"

# ... Customization ...

#set "genre" "Game"

# Fields that you want to remove
#	(to remove a field write: set "field")
