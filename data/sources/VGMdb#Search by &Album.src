# ###################################################################
# Mp3tag parsing for VGMdb.net, created by dano on 2010-01-20
#
#
# Copy this file to your sources directory at
# %APPDATA%\Mp3tag\data\sources
# Version 2.6
# 
# Changelog:
# 2.6
# [2020-07-08] Improve index search an remove the word "rated"
# 2.5
# [2020-07-04] Artist fix and add full date
# 2.4
# [2020-07-02] Tracks fix 	
# 2.3
# [2020-06-27] Publisher fix 	
# [2020-06-26] Song list fix
# ###################################################################

[Name]=VGMdb - Search by Album
[BasedOn]=vgmdb.net
[IndexUrl]=https://vgmdb.net/search?q=
[AlbumUrl]=https://vgmdb.net/album/
[WordSeparator]=+
[IndexFormat]=%Catalog%|%_url%|%Album%|%Year%|%Media%
#remove everything inside ()
[SearchBy]=$regexp(%album%,'\([^)]*\)',,1)
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################

findline "<tr rel='rel_" 1 1

ifnot "<tr rel"
	say "|"
	sayoutput "CurrentURL"
	say "|"
	say "|"
	say "|"
	#put exit for direct album tags
	exit
endif

do   
    # Catalog	
    replace "|" "/"
    findinline "span class="
    findinline ">"
    sayuntil "</span"
    say "|"
    
	   
    # URL
    findinline "https://vgmdb.net/album/"
    sayuntil "\""
    say "|"
	
    
    # Album
    findinline "class=\"albumtitle\""
    findinline "\">"
    sayuntil "</span>"
    say "|"
    
    # Year
    findinline "&month=" 1 1
    findinline "#" 1 1
    SayNChars 4
    say "."
    SayNChars 2
    say "."
    SayNChars 2
    say "|"
	
    # Media
    findinline "nowrap='nowrap'>" 
    sayuntil "</"
    saynewline
    
    moveline 1 1
    unspace
while "<tr rel='rel_" 99


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
Debug "ON" "C:\Users\egual\Desktop\vgmdbalbum.txt"

# Album
outputto "Album"
findline "class=\"albumtitle\""
replace "|" "$verticalBar()"
findinline "class=\"albumtitle\""
findinline ">"
sayuntil "</span"

# Coverurl
outputto "coverurl"
findline "id=\"coverart\""
findinline "style=\"background-image: url("
#get full image
#replace "medium-" ""
#findinline "style=\"background-image: url("
if "'"
	movechar 1
	sayuntil "'"
	
else
	findinline "../"
	sayuntil "\""
endif


# Catalog
outputto "Catalog"
findline "<b>Catalog Number</b>"
joinuntil "</tr>"
findinline "<td width=\"100%\">"
ifnot "N/A"
	if "<span id="
		findinline "#\">"
		sayuntil "</a>"
	else
		regexpreplace "<a[^>]+>" ""
		replace "</a>" ""
		RegexpReplace "\[Extra Credits].*?script>" ""
		findinline "<td width=\"100%\">"
		sayuntil "</td>"
	endif
endif

# Release Date
outputto "Year"
findline "<b>Release Date</b>"
moveline 1
sayregexp "(?<=<td>)\d{4} *(?=</td>)" "" 

if "<td><a "
	findinline "#"
	SayNChars 4
	say "."
	SayNChars 2
	say "."
	SayNChars 2
	
endif


# Publish Format
outputto "Format"
findline "<b>Publish Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Media Format
outputto "Mediatype"
findline "<b>Media Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Classification (Genre)
outputto "Genre"
findline "<b>Classification</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"


# Publisher
findline "<b>Organizations</b>"
moveline 3 1
outputto "Publisher"
say ""
if "<script type=\"text/javascript\">"
	findline "id=\"publisher_less\""	
	outputto "Publisher"
	findinline "display:inline\">"
	sayuntil "</span>"
else
if "<a href=\"/org/"
	outputto "Publisher"
	findinline "style=\"display:inline\">"
	sayuntil "</span>"
	else 
	outputto "Publisher"
	sayuntil "</td></tr>"
endif
endif

# Composer
findline "<b>Composed by</b>"
outputto "Composer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Arranged
findline "<b>Arranged by</b>"
outputto "Writer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Artist
outputto "Artist"
sayoutput "Composer"
IfNotOutput "Composer"
	outputto "Artist"
    sayoutput "Writer"
endif

# Tracks
# findline "<b>Disc 2</b></span>"
findline "class=\"smallfont\"><span class=\"label\">"
do
	outputto "Tracks"
	moveline 1
	findinline "width=\"100%\" colspan=\"2\">"
	moveline 2
	sayuntil "</td>"
	say "|"
	
	outputto "_Length"
	moveline 2
	sayregexp "(?<=<span class=\"time\">)[\d:]+(?=</span>)" ""
	say "|"

	findline "class=\"smallfont\"><span class=\"label\">" 1 1	
while "<td "

# Notes
gotoline 1
outputto "Notes"
findline "<h3>Notes"

	findline "id=\"notes\">"
	regexpreplace "<br />" " "
	replace "<i class='label'>" ""
	replace "</i>" ""
	findinline "id=\"notes\">"
	sayuntil "</div>"

# rating
findline "<span rel=\"rating\""
replace "Rated " ""
outputto "rating"
findinline "class="time\">"
sayuntil "</span>"

# comments
findline "Category"
outputto "comment"
moveline 1
sayuntil "</div>"
joinuntil "<div id=\"preview\"></div>"

if "<div style=\"margin-bottom:"
say " - "
moveline 1
findinline "\"display:inline\">"
sayuntil "</span><span"
else
say ""
endif

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# VGMdb Album URL
outputto "VGMdb Album Url"
sayoutput "CurrentUrl"

# this site dont have artist weird
# Set artistname
#outputto "artist"
#sayoutput "composer"

# ... Customization ...

#set "genre" "Game"

# Fields that you want to remove
#	(to remove a field write: set "field")
